apply plugin: 'android'
apply plugin: 'newrelic'

dependencies {
    compile 'com.android.support:support-v4:19.0.+'
    compile 'com.actionbarsherlock:actionbarsherlock:4.4.0@aar'
    compile 'org.holoeverywhere:holoeverywhere:1.6.8@aar'
    compile 'org.holoeverywhere:addon-preferences:1.6.8@aar'
    compile "com.viewpagerindicator:viewpagerindicator:2.4.1@aar"
    compile "com.facebook.android:facebook:3.5@aar"
    compile 'com.google.android.analytics:analytics:3'
    compile 'com.google.code.gson:gson:2.2.4'
    compile 'com.google.guava:guava:15.0'
    compile 'greenrobot:greendao:1.3.0-MH'
    compile 'de.greenrobot:eventbus:2.2.0'
    compile 'ch.acra:acra-mh:5.0.0-SNAPSHOT'
    compile 'com.github.kevinsawicki:http-request:5.5'
    compile 'org.apache.commons:commons-lang3:3.1'
    compile 'com.googlecode.libphonenumber:libphonenumber:5.9'
    compile 'com.github.chrisbanes.actionbarpulltorefresh:extra-abs:0.8+'
    compile 'joda-time:joda-time:2.3'
    compile 'com.android.volley:volley:1.0.1'
    compile 'com.google.code.findbugs:jsr305:2.0.2'
    compile 'javax.inject:javax.inject:1'
    compile 'com.newrelic.agent.android:android-agent:3.239.0'
}

android {
    compileSdkVersion 19
    buildToolsVersion '19'

    defaultConfig {
        minSdkVersion 8
        targetSdkVersion 19
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }
    }

    signingConfigs { release }

    productFlavors {
        google {}
        amazon {}
        bn {}
    }

    buildTypes {
        release {
            runProguard true
            proguardFile getDefaultProguardFile('proguard-android.txt')
            proguardFile 'proguard.txt'
            signingConfig signingConfigs.release
        }
        debug {}
    }
}

// Dynamic Signing
if (project.hasProperty("signingStoreFile") && project.hasProperty("signingStorePassword") && project.hasProperty("signingKeyAlias") && project.hasProperty("signingKeyPassword")) {
    android.signingConfigs.release.storeFile = file(project.signingStoreFile)
    android.signingConfigs.release.storePassword = project.signingStorePassword
    android.signingConfigs.release.keyAlias = project.signingKeyAlias
    android.signingConfigs.release.keyPassword = project.signingKeyPassword
} else {
    logger.warn("WARNING: Invalid release signingConfig properties, signing with debug key.")
    android.buildTypes.release.signingConfig = android.buildTypes.debug.signingConfig
}

// Dynamic Versioning
android.defaultConfig.versionCode = versionCode
android.defaultConfig.versionName = versionName