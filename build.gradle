apply plugin: 'versions'

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:0.5.+'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.4'
    }
}

version = "2.8"
group = "com.missionhub"

allprojects {

    ext {
        buildNumber = getBuildNumber()
        versionCode = getVersionCode()
        versionName = getVersionName()
        isSnapshotBuild = isSnapshotBuild()
    }

    repositories {
        maven { url 'http://nexus.cr-wd.com/content/repositories/releases/' }
        maven { url 'http://nexus.cr-wd.com/content/repositories/snapshots/' }
        mavenCentral()
    }

}

apply plugin: 'android-reporting'

def getVersionName() {
    if (getVersionCode() == -1) {
        return version
    } else {
        return version + "." + getVersionCode()
    }
}

def getVersionCode() {
    if (getBuildNumber() >= 1) {
        return getBuildNumber();
    }
    return -1;
}

def getBuildNumber() {
    def number = System.getenv("BUILD_NUMBER");
    if (number != null && !("".equals(number))) {
        return Integer.parseInt(number)
    }
    return getNextBuildNumber()
}

def getNextBuildNumber() {
    try {
        def jobXml = (new XmlParser()).parse("http://mh.cr-wd.com/ci/job/MissionHub%20Android/api/xml");
        return Integer.parseInt(jobXml.nextBuildNumber.text());
    } catch (Exception e) {
        e.printStackTrace();
    }
    return -1;
}

def isSnapshotBuild() {
    return version.contains("SNAPSHOT") == true;
}

// debug vars
println "VERSION: " + version
println "BUILD: " + buildNumber
println "VERSION CODE: " + versionCode
println "VERSION NAME: " + versionName
println "IS SNAPSHOT: " + isSnapshotBuild